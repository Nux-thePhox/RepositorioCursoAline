-- TRIGGER MULTIPLE DE TIPO AFTER Y A NIVEL FILA

/*SE PUEDEN DESACTIVAR TRIGERS POR SENTENCIA O POR CONEXIONES*/
-- DESACTIVAR TRIGGERS SIMPLES
ALTER TRIGGER TR_I_CEL DISABLE; -- POR SENTENCIA
/*Revisa en el apartado de disparadores para verificar que el trigger ya esta
desactivado*/

/*Desactivarlo por conexiones se debe de buscar el trigger creado en el apartado
de disparadores, dar clic derecho en el trigger y seleccionar la opcion 
desactivar*/

--TRIGGER QUE ACTUARA CUANDO OCURRA UNA OPERACION DE TIPO INSERT, DELETE OR 
-- UPDATE
CREATE OR REPLACE TRIGGER TR_MULTIPLE_CEL
AFTER INSERT OR UPDATE OR DELETE ON CELULAR
FOR EACH ROW
DECLARE
    LV_ID_BIT NUMBER;
    LV_USUARIO NVARCHAR2(100);
    LV_FECHA DATE;
BEGIN
    --RECUPERAR UN ID VALIDO USANDO SECUENCIAS
    SELECT ID_BIT_CEL_SEQ.NEXTVAL, USER, SYSDATE
    INTO LV_ID_BIT, LV_USUARIO, LV_FECHA 
    FROM DUAL;
    
    IF INSERTING THEN
        INSERT INTO CELULAR_BIT(ID_BIT, ID_CELULAR, MARCA, MODELO, RAM, 
            PROCESADOR, PRECIO, MARCA_OLD, MODELO_OLD, RAM_OLD, PROCESADOR_OLD, 
            PRECIO_OLD, USUARIO, FECHA_MOV, MOVIMIENTO)
        VALUES (LV_ID_BIT, :NEW.ID_CELULAR, :NEW.MARCA, :NEW.MODELO, :NEW.RAM, 
            :NEW.PROCESADOR, :NEW.PRECIO, NULL, NULL, NULL, 
            NULL, 0, LV_USUARIO, LV_FECHA, 'INSERT');
    ELSIF UPDATING THEN
        INSERT INTO CELULAR_BIT(ID_BIT, ID_CELULAR, MARCA, MODELO, RAM, 
            PROCESADOR, PRECIO, MARCA_OLD, MODELO_OLD, RAM_OLD, PROCESADOR_OLD, 
            PRECIO_OLD, USUARIO, FECHA_MOV, MOVIMIENTO)
        VALUES (LV_ID_BIT, :NEW.ID_CELULAR, :NEW.MARCA, :NEW.MODELO, :NEW.RAM, 
            :NEW.PROCESADOR, :NEW.PRECIO, :OLD.MARCA, :OLD.MODELO, :OLD.RAM, 
            :OLD.PROCESADOR, :OLD.PRECIO, LV_USUARIO, LV_FECHA, 'UPDATE');
    ELSIF DELETING THEN
        INSERT INTO CELULAR_BIT(ID_BIT, ID_CELULAR, 
            MARCA_OLD, MODELO_OLD, RAM_OLD, PROCESADOR_OLD, 
            PRECIO_OLD, USUARIO, FECHA_MOV, MOVIMIENTO)
        VALUES (LV_ID_BIT, :OLD.ID_CELULAR, :OLD.MARCA, :OLD.MODELO, :OLD.RAM, 
            :OLD.PROCESADOR, :OLD.PRECIO, LV_USUARIO, LV_FECHA, 'DELETING');
    END IF;
END TR_MULTIPLE_CEL;